class Match {

  field Fighter Player, Bot;
  field AIController ai;

  constructor Match new(){
    let Player = Fighter.new();
    let Bot = Fighter.new();
    return this;
  }

  method void dispose() {
      do Player.dispose();
      do Bot.dispose();
      do Memory.deAlloc(this);
      return;
   }

  method void beginMatch(){

    do Player.setPosition(120);
    do Bot.setPosition(340);
    do Player.setOpponent(Bot);
    do Bot.setOpponent(Player);
    let ai = AIController.new(Bot);
    return;
  }

  method void run(){
    var char key;
    var boolean exit;
    let exit = false;

    do beginMatch();
    while (~exit) {

      let key = Keyboard.keyPressed();
      if (key = 81) { let exit = true; } // Q Key -> Quit
      else { do Player.handleInput(key); }
      do ai.update();
      do Output.moveCursor(0, 0);
      do Screen.clearScreen();
      do Bot.update();
      do Player.update();

      if (Player.dead() | Bot.dead()){
        let exit = true;
        do Sys.wait(1000);
        do Screen.clearScreen();
        do Output.moveCursor(12, 22);
        if (Player.dead()){
          do Log.logMessage("// Bot Wins! >:) //");
        } else {
          do Log.logMessage("// Player Wins! \\o/ \\o/ //");
        }
      }
      do Sys.wait(20);
    }
    do Sys.wait(2000);
    return;
  }

}