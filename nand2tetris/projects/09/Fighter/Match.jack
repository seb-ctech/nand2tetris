class Match {

  field Fighter Player, Bot;
  field AIController ai;

  constructor Match new(){
    let Player = Fighter.new();
    let Bot = Fighter.new();
    let ai = AIController.new(Bot);
    return this;
  }

  method void dispose() {
      do Player.dispose();
      do Bot.dispose();
      do Memory.deAlloc(this);
      return;
   }

  method void beginMatch(){

    do Player.setPosition(120, 50);
    do Bot.setPosition(340, 50);
    do Player.setOpponent(Bot);
    do Bot.setOpponent(Player);
    return;
  }

  method void run(){
    var char key;
    var boolean exit;
    let exit = false;

    do beginMatch();
    while (~exit) {

      let key = Keyboard.keyPressed();
      if (key = 81) { let exit = true; } // Q Key -> Quit
      else { do Player.handleInput(key); }
      do ai.update();
      do Bot.update();
      do Player.update();

      if (Player.dead() | Bot.dead()){
        let exit = true;
      }
      do Sys.wait(20);
    }

    return;
  }

}