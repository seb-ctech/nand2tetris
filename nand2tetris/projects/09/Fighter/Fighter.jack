class Fighter {

  field boolean wasHit;
  field boolean activeHit;
  field boolean isPlayer;
  field boolean blocking;
  field Square body;
  field Fighter target;
  field int speed;
  field Hit hit;

  constructor Fighter new(){
    let wasHit = false;
    let activeHit = false;
    let speed = 10;
    let body = Square.new(0, 0, 40, 90);
    return this;
  }

  method void setOpponent(Fighter other){
    let target = other;
    return;
  }

  method void setPosition(int x){
    var int height, ycoord;
    let ycoord = Application.screenHeight();
    let height = body.getSizeY();
    do body.setPosition(x, ycoord - height);
    return;
  }

  method boolean dead(){ return wasHit; }

  method Square getBody() { return body; }

  method Square getTarget() { return target; }

  method boolean isHitting() { return activeHit; }

  method boolean isBlocking(){ return blocking; }

  method void hit() {
    let wasHit = true;
    return;
  }

  method int getTargetDir(){
    var int offset;
    var Square targetBody;
    let targetBody = target.getBody();
    let offset = body.getX() - targetBody.getX();
    if (offset < 0){ return -1; }
    else { return 1; }
  }

  method int getDistanceOpp(){
    var int ownX, oppX, diff;
    var Square oppBody;
    let oppBody = target.getBody();
    if (getTargetDir() < 0){
      let ownX = body.getRight(); 
      let oppX = oppBody.getX();
    } else {
      let ownX = body.getX(); 
      let oppX = oppBody.getRight();
    }
    let diff = ownX - oppX;
    if (diff < 0){
      return diff * -1;
    } else {
      return diff;
    }
    
  }

  method void dispose() {
      do body.dispose();
      if(activeHit){
        do hit.dispose();
      }
      do Memory.deAlloc(this);
      return;
   }

  method void update(){
    if (activeHit){
      do hit.update();
      if(hit.hits(target) & (~ target.isBlocking())){
        do target.hit();
      }
      if (hit.finished()){
        let activeHit = false;
        do hit.dispose();
      }
    }
    do body.draw();
    return;
  }

  method void handleInput(char key){
    let blocking = false;
    // Spacebar -> Strong Hit
    if(key = 32) {
      if (~ (activeHit)){
        do tryAttack(true);
      }
    }
    // C -> Quick Hit
    if(key = 67) {
      do tryAttack(false);
    }
    // Left Arrow -> Move Left
    if (key = 130){
      do move(-1);
    }
    // Right Arrow -> Move Right
    if (key = 132){
      do move(1);
    }
    return;
  }

  method void move(int dir){
    var int newX;
    var Square targetBody;
    let newX = body.getX() + (dir * speed);
    
    if( (newX > 0) & ( (newX + body.getSizeX()) < Application.screenWidth())){
      do setPosition(newX);
    } 
    
    let targetBody = target.getBody();

    if( ((dir < 0) & (targetBody.getX() > body.getX())) | ( (dir > 0) & (targetBody.getX() < body.getX()) ) ){
      let blocking = true;
    }

    return;
  }

  method void tryAttack(boolean s){
    var Square targetBody;
    let targetBody = target.getBody();
    if((~ body.collides(targetBody))){
      if (~ (activeHit)){
        if(s){
          do attackStrong();
        } else {
          do attackQuick();
        }
      }
    }
    return;
  }

  method void attackQuick(){
    let hit = Hit.new(this, false);
    let activeHit = true;
    return;
  }

  method void attackStrong(){
    let hit = Hit.new(this, true);
    let activeHit = true;
    return;
  }

}